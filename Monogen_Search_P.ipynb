{
 "cells": [
  {
   "cell_type": "markdown",
   "id": "82595d4e-afe1-4bf8-a778-024381c40cac",
   "metadata": {},
   "source": [
    "This notebook provides an algorithm for finding all monogenizations of a quartic order defined by a polynomial.\n",
    "A ring is monogenic if it is generated by a single element as a $\\mathbb{Z}$ algebra.\n",
    "If $R$ is a monogenic ring, with $\\mathbb{Z}$ algebra generator $\\xi$, then we say $\\xi$ is a monogenizer of $R$.\n",
    "Note that if $\\xi$ is a monogenizer of a ring $R$ then $\\pm \\xi + c$ will also be a monogenizer for any $c \\in \\mathbb{Z}$, in this case we say that $\\xi$ and $\\pm \\xi +c$ are $\\mathbb{Z}$ equivalent.\n",
    "A $\\mathbb{Z}$ equivalence class is called a monogenization.\n",
    "\n",
    "\n",
    "Let $f \\in \\mathbb{Z}[t]$ be irreducible and monic, then the ring $R=\\mathbb{Z}[t]/(f(t))$ is a domain and is generated by $t$ as a $\\mathbb{Z}$ algebra.\n",
    "If $\\xi$ is any root of $f$ then $R$ will be isomorphic to $\\mathbb{Z}[\\xi]$. \n",
    "The code here is an implementation of the algorithm given by István Gaál, Attila Pethő, Michael Pohst (see https://www.sciencedirect.com/science/article/pii/S0022314X96900359).\n",
    "The output will be a triple $(x, y, z) \\in \\mathbb{Z}^3$ which gives the common coordinates for every element of a monogenization of $\\mathbb{Z}[\\xi]$ in the basis $\\{1, \\xi, \\xi^2, \\xi^3\\}$.\n",
    "\n",
    "\n",
    "\n",
    "\n",
    "\n",
    "Code was written for SageMath 10.5\n",
    "\n",
    "\n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 56,
   "id": "9ebccfb3",
   "metadata": {
    "jupyter": {
     "source_hidden": true
    }
   },
   "outputs": [],
   "source": [
    "#Run this cell first\n",
    "\n",
    "# import random\n",
    "import itertools\n",
    "from sage.quadratic_forms.qfsolve import qfsolve\n",
    "\n",
    "\n",
    "A= ZZ['t']\n",
    "t=A.gen()\n",
    "# t is needed to use some sageMath functions, particularly the is_irreducable() function.\n",
    "R.<T, S,X , Y, Z> = PolynomialRing(QQ)\n",
    "#This ring is needed when solving thue equations\n",
    "# P.<X,Y,Z> =QQ[]\n",
    "\n",
    "#For now this has a dictionary will every irreducible poly, in the selected range. Later This will hopefully involve some spl, ideas\n",
    "# The key C returns an array of the coefficients checked so far.\n",
    "\n",
    "\n",
    "def solvethue(f,val_list):\n",
    "    #this calculation takes a polynomial and a list of values, and finds all solutions to the thue equation F=val, for val in val_list\n",
    "    #we are using the GRH flag to speed up the calculation, we then check the end results\n",
    "    out=[]\n",
    "    F=gp.thueinit(f,1)\n",
    "    for val in val_list:\n",
    "        out=out+gp.thue(F,val).sage()\n",
    "    return out\n",
    "#This function will find all integral solutions to a thue equation, it calls the thue solver in Pair/gp\n",
    "\n",
    "# def solvethue(f,n):\n",
    "#     assert f.is_homogeneous()\n",
    "#     return gp.thue( gp.thueinit(f), n).sage()\n",
    "\n",
    "def Qform_solve(M):\n",
    "# the input here is a gram matrix of a trinary quadratic form\n",
    "    sol= qfsolve(M)\n",
    "    #qfsolve will return a solution to the quadratic form or an integer corresponding to the place where the form is unsolvable\n",
    "    if isinstance(sol, (int, Integer)):\n",
    "        return(False)\n",
    "    else:\n",
    "        return(sol)\n",
    "\n",
    "####################################################################################################################################\n",
    "#this class if for a quartic thue equation, it is used in the main class poly.\n",
    "class quartic_thue:\n",
    "    def __init__(self):\n",
    "        self.equation = None\n",
    "        self.fx = None\n",
    "        self.fy = None\n",
    "        self.fz= None\n",
    "        self.k_list = []\n",
    "        \n",
    "    \n",
    "    def reset(self):\n",
    "        self.__init__()\n",
    "\n",
    "########################################################################################################################################\n",
    "\n",
    "def find_monogens(num_list):\n",
    "    if len(num_list)==5:\n",
    "        P=poly(num_list)\n",
    "        f_p=P.make(P.coeff)\n",
    "        if f_p.is_irreducible(): \n",
    "            P.monogen_test()\n",
    "            print( P.solu_coords)\n",
    "            # print(P.signature)\n",
    "        else:\n",
    "            print(f'the polynomial defined by {num_list} is reducible')\n",
    "    else:\n",
    "        print('the polynomial must be degree 4')\n",
    "\n",
    "#################################################################################################################################3\n",
    "class poly:\n",
    "    def __init__(self, coeff ):\n",
    "        self.coeff =coeff \n",
    "        # an array that recorded the coefficients of the poly,\n",
    "        # we choose to have 5 entries even though the first is always 1, \n",
    "        #to make recalling coefficients index match mathematical labeling\n",
    "        self.cubic_res= self.cubic_resolvent()\n",
    "        #a trinary quadratic from coming from the index form equation\n",
    "        self.Q1= self.index_equations(1)\n",
    "        # a trinary quadratic from coming from the index form equation\n",
    "        self.Q2=self.index_equations(2)\n",
    "        #the coord\n",
    "        self.cubic_coords= set()\n",
    "        self.solu_coords={}\n",
    "        #a dictionary that records an array of the p-coord attached to each cubic solution\n",
    "        #the cubic solutions are the keys\n",
    "        #this dictionary has keys the cubic solutions and records if we have looked for type I solutions\n",
    "        self.quar_number=0\n",
    "        self.cubic_number=0\n",
    "        self.QT=quartic_thue()\n",
    "        #This holds a binary quartic form and information about how it relates to the solution to the index form\n",
    "        #it is reset after each use to reduce the memory need for the whole calculation\n",
    "        self.signature=None\n",
    "\n",
    "        \n",
    "        \n",
    "    def reset(self,new_coeff):\n",
    "         self.__init__(new_coeff)\n",
    "        \n",
    " \n",
    "    def cubic_resolvent(self):\n",
    "            return( [1,-self.coeff[2], self.coeff[1]*self.coeff[3] - 4*self.coeff[4], \n",
    "                     4*self.coeff[2]*self.coeff[4]-self.coeff[3]**2-self.coeff[1]**2*self.coeff[4]])\n",
    "            \n",
    "    \n",
    "    def index_equations(self,i):\n",
    "        A=self.coeff\n",
    "        if i ==1:\n",
    "            Q_1=X**2-A[1]*X*Y + A[2]*Y**2+X*Z*(A[1]**2-2*A[2])+Y*Z*(A[3]-A[1]*A[2])+Z**2*(-A[1]*A[3]+A[2]**2 +A[4])\n",
    "            return(Q_1)\n",
    "        if i ==2:\n",
    "            Q_2 = Y**2 - X*Z - A[1]*Y*Z+A[2]*Z**2\n",
    "            return(Q_2)\n",
    "\n",
    "    \n",
    "    def make(self, coeff_list):\n",
    "        #converts a list coeff to a univariable polynomial for operation in sage\n",
    "        L=len(coeff_list)\n",
    "        f= sum((coeff_list[i])* t**(L-1-i) for i in range(L))\n",
    "        return(f)\n",
    "    \n",
    "    def make_form(self, coeff_list):\n",
    "        #converts a list of coeff to a binary form\n",
    "        L=len(coeff_list)\n",
    "        F= sum(coeff_list[i]* (S**i)*(T**(L-1-i)) for i in range(L))\n",
    "        return(F)\n",
    "    \n",
    "    def typeI_cubic(self):\n",
    "        #this should find the solution to the cubic thue\n",
    "            c_list= self.cubic_res\n",
    "            L=len(c_list)\n",
    "            f= self.make(self.cubic_res)\n",
    "            Solu=solvethue(f,[1])\n",
    "            #since f is an odd function we only need to check the positive solutions\n",
    "            self.cubic_numberr=len(Solu)\n",
    "            for I in Solu:\n",
    "                t_1=I[0]\n",
    "                t_2=I[1]\n",
    "                self.solu_coords[(t_1,t_2)]=False\n",
    "                point = (t_1+c_list[1]*t_2, t_2)\n",
    "                self.cubic_coords.add(point)\n",
    "                #the value is false let us know that we have not tested for quar monogen over this cubic monogen yet.\n",
    "    \n",
    "    def find_quar_thue(self,sol_cub):\n",
    "        if sol_cub==(1,0) or sol_cub== (-1,0):\n",
    "            f_x=T**2 + self.coeff[1]*T*S+ self.coeff[2]*S**2\n",
    "            f_y = T*S+ self.coeff[1]*S**2\n",
    "            f_z = S**2\n",
    "            self.QT.equation= self.make_form(self.coeff)\n",
    "            self.QT.fx=f_x\n",
    "            self.QT.fy=f_y\n",
    "            self.QT.fz=f_z\n",
    "            self.QT.k_list=[1]\n",
    "            return(True)\n",
    "        else:\n",
    "            u=sol_cub[0]\n",
    "            v=sol_cub[1]\n",
    "            \n",
    "            Q_1=self.Q1\n",
    "            Q_2 = self.Q2\n",
    "            #these are the quadratic from the index system of equations\n",
    "            d, l,k = xgcd(u,v)\n",
    "            Q_u=l*Q_1+k*Q_2\n",
    "            Q_v = v*Q_1-u*Q_2\n",
    "            #We are finding the coefficient of Q_v\n",
    "            #this section could be improved\n",
    "            a=Q_v.subs(X=1,Y=0, Z=0)\n",
    "            b=Q_v.subs(X=1, Y=1, Z=0) - Q_v.subs(X=1, Y=0, Z=0) -Q_v.subs(X=0, Y=1, Z=0) \n",
    "            c= Q_v.subs(X=1, Y=0,Z=1) - Q_v.subs(X=1, Y=0, Z=0)- Q_v.subs(X=0, Y=0, Z=1)\n",
    "            d= Q_v.subs(X=0, Y=1,Z=0)\n",
    "            e=Q_v.subs(X=0, Y=1,Z=1) -Q_v.subs(X=0, Y=1, Z=0) - Q_v.subs(X=0, Y=0, Z=1) \n",
    "            f=Q_v.subs(X=0, Y=0,Z=1)\n",
    "            M_v=Matrix(QQ, [[a, b/2, c/2], [b/2, d, e/2], [c/2, e/2, f]])\n",
    "            qfsol=Qform_solve(M_v)\n",
    "            \n",
    "            if qfsol== False:\n",
    "                self.QT.reset()\n",
    "                return(False)\n",
    "            #this is one of the fail points for no quartic monogenizers existing for a given cubic monogenizer\n",
    "\n",
    "            else:\n",
    "                x_0=qfsol[0] ; y_0= qfsol[1] ; z_0= qfsol[2]\n",
    "                #the next polys parameterize the solution to Q_v=0\n",
    "                #note only one of y_0 or z_0 can be zero else our cubic solution is (1,0)\n",
    "                if z_0!=0:\n",
    "                    L_1=2*a*x_0 + b*y_0 +c*z_0 ; L_2 = b*x_0 + 2*d*y_0 + e*z_0\n",
    "                    f_x= (-a*x_0 + L_1)*T**2 + (-b*x_0 + L_2)*T*S + (-d*x_0)*S**2\n",
    "                    f_y= (-a*y_0)*T**2+ (-b*y_0 + L_1)*T*S+ (-d*y_0+L_2)*S**2\n",
    "                    f_z = (-a*z_0)*T**2 + (-b*z_0)*T*S + -d*z_0*S**2\n",
    "                else:\n",
    "                    L_1=2*a*x_0 + b*y_0 + c*z_0 ; L_2 = c*x_0 + e*y_0 + 2*f*z_0\n",
    "                    f_x= (-a*x_0 +L_1)*T**2 + (-c*x_0 + L_2)*T*S + (-f*x_0)*S**2\n",
    "                    f_y = (-a*y_0)*T**2 + (-c*y_0)*T*S + (-f*y_0)*S**2\n",
    "                    f_z = (-a*z_0)*T**2 + (-c*z_0+ L_1)*T*S + (-f*z_0 + L_2)*S**2\n",
    "                #now we need to find the value of the multiple k\n",
    "                coeff_x = [f_x.subs(T=1, S=0), f_x.subs(T=1, S=1)- f_x.subs(T=1, S=0) -f_x.subs(T=0, S=1), f_x.subs(T=0, S=1)]\n",
    "                coeff_y = [f_y.subs(T=1, S=0), f_y.subs(T=1, S=1)- f_y.subs(T=1, S=0) -f_y.subs(T=0, S=1), f_y.subs(T=0, S=1)]\n",
    "                coeff_z = [f_z.subs(T=1, S=0), f_z.subs(T=1, S=1)- f_z.subs(T=1, S=0) -f_z.subs(T=0, S=1), f_z.subs(T=0, S=1)]\n",
    "\n",
    "                coeff_mat=matrix([coeff_x, coeff_y, coeff_z])\n",
    "                mult=coeff_mat.det()/(gcd(coeff_x+coeff_y+coeff_z))\n",
    "                self.QT.k_list=divisors(mult)\n",
    "                \n",
    "\n",
    "                #here we are recording the information that is needed to find the coordinates\n",
    "                self.QT.equation= Q_2.subs(X=f_x, Y=f_y, Z= f_z)\n",
    "                self.QT.fx=f_x\n",
    "                self.QT.fy=f_y\n",
    "                self.QT.fz=f_z\n",
    "\n",
    "                return(True)\n",
    "\n",
    "            \n",
    "            \n",
    "    def coord_check(self, point, sol_cub):\n",
    "        bool1=((P.Q1.subs(X=point[0],Y=point[1],Z=point[2]),P.Q2.subs(X=point[0],Y=point[1],Z=point[2])) == sol_cub) \n",
    "        bool2=((-1*P.Q1.subs(X=point[0],Y=point[1],Z=point[2]),-1*P.Q2.subs(X=point[0],Y=point[1],Z=point[2])) == sol_cub)\n",
    "        return(bool1 or bool2)\n",
    "    \n",
    "    def typeI_quar(self):\n",
    "        # This function takes a solution to the cubic and finds all coordinates that give a quartic monogenizer over that cubic\n",
    "        #the output is stored as a value in the dictionary self.coords\n",
    "        # we store the value of True if no solution is found, this indicates that the test was run\n",
    "        for sol_cub in self.solu_coords.keys():\n",
    "            if self.solu_coords[sol_cub]==False:\n",
    "                val=self.find_quar_thue(sol_cub)\n",
    "                if val== True:\n",
    "                    f=self.QT.equation\n",
    "                    if sol_cub[1]==0:\n",
    "                        quar_solutions=solvethue(f.subs(S=1),[-1,1])\n",
    "                    elif sol_cub[1]!= 0:\n",
    "                        val_list=self.QT.k_list +[-1* i for i in self.QT.k_list]\n",
    "                        quar_solutions=solvethue(f.subs(S=1),val_list)\n",
    "                        \n",
    "                    if quar_solutions==[]:\n",
    "                        self.solu_coords[sol_cub]= 0\n",
    "                        #the true here indicates that the val has been tested and that no solutions were found\n",
    "                    else:\n",
    "                        coord_list=[]\n",
    "                        for I in quar_solutions:\n",
    "                            t_1=I[0]\n",
    "                            s_1=I[1]\n",
    "                            point=(self.QT.fx.subs(T=t_1, S=s_1), self.QT.fy.subs(T=t_1, S=s_1), self.QT.fz.subs(T=t_1, S=s_1))\n",
    "                            if self.coord_check(point,sol_cub):\n",
    "                                coord_list.append(point)\n",
    "                        coord_set= set(coord_list)\n",
    "                        self.solu_coords[sol_cub]=coord_set\n",
    "                        self.quar_number= self.quar_number + len(coord_set)\n",
    "                elif val==False:\n",
    "                    self.solu_coords[sol_cub]= 0\n",
    "                    #the true here indicates that the val has been tested and that no solution were found\n",
    "\n",
    "    \n",
    "    def find_signatur(self):\n",
    "        out=[]\n",
    "        for I in self.solu_coords.keys():\n",
    "            if self.solu_coords[I]==0:\n",
    "                out.append(0)\n",
    "            else:\n",
    "                out.append(len(self.solu_coords[I]))\n",
    "        self.signatur=out\n",
    "        \n",
    "    def monogen_test(self):\n",
    "        #calling this method will find all the mongenizations of the ring defined by the input polynomial\n",
    "        #we will return the signature which \n",
    "        self.typeI_cubic()\n",
    "        self.typeI_quar()\n",
    "        self.find_signatur()\n",
    "        return(self.signature)\n",
    "\n"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "4ba20dd8-09a2-4819-b608-cc0c196423b9",
   "metadata": {},
   "source": [
    "Input: An array $[a_1, a_2, a_3, a_4, a_4]$ of integers.\n",
    "        Soon I will add the ability to have inputs of degree 3.\n",
    "\n",
    "Constraints:The polynomal $f(t) =  t^4 + a_1 t^3 + a_2 t^2 + a_3 t + a_4$ must be irreducible.\n",
    "\n",
    "Output: A dictionary that pairs the solutions to the cubic resolvent thue equations with a set of integer-valued triples $(x,y,z)$, such that $c + xt + yt^2 + zt^3$ is a monogenizer of the ring $\\mathbb{Z}[t]/(f(t)), and every monogenization will be represented by exactly one triple in the output."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 57,
   "id": "d7f91fdc-1e23-4114-8e6e-57d4d6dab64c",
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "{(-1, -1): {(0, 0, -1), (1, 1, 0), (0, 1, 0)}, (1, 0): {(1, 0, 0), (1, 0, 1), (1, 1, 1)}}\n"
     ]
    }
   ],
   "source": [
    "find_monogens([1,1,1,1,1])"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "58453a4b-4e91-4847-932e-fe4b2a16aa1f",
   "metadata": {},
   "outputs": [],
   "source": []
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "SageMath 10.5",
   "language": "sage",
   "name": "sagemath"
  },
  "language_info": {
   "codemirror_mode": {
    "name": "ipython",
    "version": 3
   },
   "file_extension": ".py",
   "mimetype": "text/x-python",
   "name": "python",
   "nbconvert_exporter": "python",
   "pygments_lexer": "ipython3",
   "version": "3.12.5"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
